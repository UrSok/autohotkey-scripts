#SingleInstance Force
#HotIf WinActive("DARK SOULSâ„¢: REMASTERED")

Speaker := ComObject("SAPI.SpVoice")
MessageBoxTitle := "Dark Souls Read Only Save Macro"
SaveFile := ""

; Uncomment to skip the save file selection
; SaveFile := "C:\Users\marda\OneDrive\Documents\NBGI\DARK SOULS REMASTERED\110507413\DRAKS0005.sl2"

MsgBox("See https://github.com/UrSok/autohotkey-scripts?tab=readme-ov-file#dark-souls-remastered-read-only-save to understand how to use.", MessageBoxTitle, 48)
SelectSave()

F1:: {
    if !IsSaveValid() {
        ExecuteFileNotValidFlowInMacro() 
        return
    }

    FileAttributes := FileGetAttrib(SaveFile)
    if !InStr(FileAttributes, "R") {
        FileSetAttrib("+R", SaveFile)
        Speaker.Speak("Read-only enabled")
    } else {
        FileSetAttrib("-R", SaveFile)
        Speaker.Speak("Read-only disabled")
    }
}

F3:: {
    if !IsSaveValid() {
        ExecuteFileNotValidFlowInMacro() 
        return
    }

    FileAttributes := FileGetAttrib(SaveFile)
    if !InStr(FileAttributes, "R") {
        Speaker.Speak("Read-only is not enabled")
        return
    }

    SetKeyDelay(200)
    SendEvent("{ESC}{Left}{E}{Up}{E}{Left}{E}")
    ; SendMultipleKeysWithDelay("{ESC};{Left};{E};{Up};{E};{Left};{E}", 200)
    FileSetAttrib("-R", SaveFile)
    Sleep(3000)
    SetKeyDelay(500)
    SendEvent("{E}{E}{E}{E}{E}{E}{E}")
    ; SendMultipleKeysWithDelay("{E};{E};{E};{E};{E};{E};{E}", 500)
    FileSetAttrib("+R", SaveFile)
}

; SendMultipleKeysWithDelay(keys, delay) {
;     loop parse, keys, ";" {
;         SendInput(A_LoopField)
;         Sleep(delay)
;     }
; }

IsSaveValid() {
    return SaveFile != "" AND FileExist(SaveFile)
}

SelectSave() {
    if IsSaveValid()
        return

    SaveFileLocal := FileSelect(1, , "Select Dark Souls Save File", "Dark Souls Save File (*.sl2)")
    if InStr(SaveFileLocal, ".sl2", 1, StrLen(SaveFileLocal) - 5) > 0
        global SaveFile := SaveFileLocal
}

ExecuteFileNotValidFlowInMacro() {
    MsgBox("No save file found. Please select one and then re-use the macro!", MessageBoxTitle, 16)
    SelectSave()
}
